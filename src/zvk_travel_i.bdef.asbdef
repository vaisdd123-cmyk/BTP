managed implementation in class zbp_vk_travel_i unique;
strict ( 2 ); // this ensure we are using the strict syntax

with draft;

define behavior for ZVK_TRAVEL_I alias Travel
persistent table zvk_travel
draft table ZVK_draft_T
lock master
total etag LastChangedAt
authorization master ( global , instance )
etag master LocalLastChangedAt // system detect the latest timestamp and prevent overwrite
{
  create; // user can create new travels
  update; // user can update or edit the existing travels
  delete; // user can cancel/delet the entire travel ( including its child booking and bookingsupp)
  field (numbering : managed , readonly ) TravelUuid;  // non editable field on the fiori screen
  field(readonly) TravelId;
  association _Booking { create; with draft; }    // users can do the bookings to the current travel

  draft action Edit;
  draft action Discard;
  draft action Resume ;
  draft action Activate optimized;
  draft determine action Prepare ;

  // Determination

  determination setTravelId on save { create;update; }
  determination setOverallStatus on modify { create; update;}

  // Action

  action acceptTravel result [1] $self;
  action rejectTravel result [1] $self;

  action deductDiscount parameter ZVK_Discount result [1] $self { default function GetDefaultsForDeductDiscount; }

  mapping for ZVK_TRAVEL {
  TravelUuid = travel_uuid;
  TravelId = travel_id;
  AgencyId = agency_id;
  CustomerId = customer_id;
  BeginDate = begin_date;
  EndDate = end_date;
  BookingFee = booking_fee;
  TotalPrice = total_price;
  CurrencyCode = currency_code;
  Description = description;
  OverallStatus = overall_status;
  LocalCreatedBy = local_created_by;
  LocalCreatedAt = local_created_at;
  LocalLastChangedBy = local_last_changed_by;
  LocalLastChangedAt = local_last_changed_at;

  }
}

define behavior for ZVK_BOOKING_I alias Booking
persistent table zvk_booking
draft table ZVK_Draft_B
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) TravelUuid , BookingId;
  field ( readonly ) BookingUuid;
  association _Travel {with draft;}
  association _BookingSupplement { create; with draft;}

  determination setBookingId on save { create;}
  determination setBookinDate on save { create; }

  mapping for ZVK_Booking{
  BookingUuid = booking_uuid;
  TravelUuid = parent_uuid;
  BookingId = booking_id;
  BookingDate = booking_date;
  CustomerId = customer_id;
  CarrierId = carrier_id;
  ConnectionId = connection_id;
  FlightDate = flight_date;
  FlightPrice = flight_price;
  CurrencyCode = currency_code;
  BookingStatus = booking_status;
  LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZVK_BOOKINGSUP_I alias BookingSupplement
persistent table zvk_bookingsup
draft table zvk_draft_bs
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed , readonly ) BookingsupUuid;
  field ( readonly : update) BookingUuid , TravelUuid;
  field ( readonly ) BookingSuppId;

  association _Travel {with draft;}
  association _Booking {with draft;}

  determination SetBookingSuppId on save { create; }


  mapping for ZVK_BOOKINGSUP {

  BookingsupUuid = bookingsup_uuid;
  BookingSuppId = booking_supp_id;
  TravelUuid = root_uuid;
  BookingUuid = parent_uuid;
  SupplementId = supplement_id;
  Price = price;
  CurrencyCode = currency_code;
  LocalLastChangedAt = local_last_changed_at;

  }
}